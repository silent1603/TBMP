cmake_minimum_required(VERSION 3.18)
project(TBMP-server C CXX)

# -----------------------------
# Paths
# -----------------------------
set(BIN_DIR "${CMAKE_SOURCE_DIR}/bin")
set(SOURCES_DIR "${CMAKE_SOURCE_DIR}/sources")
set(TOOLS_DIR "${CMAKE_SOURCE_DIR}/tools")
set(NINJA_BIN "${TOOLS_DIR}/ninja/bin/ninja")
set(ISPC_DIR "${TOOLS_DIR}/ispc")

set(CMAKE_PREFIX_PATH "${ISPC_DIR}" ${CMAKE_PREFIX_PATH})

# Make Ninja available to CMake
if(EXISTS "${NINJA_BIN}")
    if(WIN32)
        set(NINJA_BIN "${NINJA_BIN}.exe")
    endif()
    set(CMAKE_MAKE_PROGRAM "${NINJA_BIN}" CACHE FILEPATH "Ninja executable")
endif()

find_package(Threads REQUIRED)
find_package(ispc REQUIRED)
# -----------------------------
# Compiler settings
# -----------------------------
set(CMAKE_C_STANDARD 11)
set(CMAKE_C_STANDARD_REQUIRED ON)

foreach(dir RUNTIME LIBRARY ARCHIVE)
    set(CMAKE_${dir}_OUTPUT_DIRECTORY "${BIN_DIR}")
endforeach()

if(MSVC)
    string(APPEND CMAKE_C_FLAGS_DEBUG " /Zi")
    string(APPEND CMAKE_CXX_FLAGS_DEBUG " /Zi")
    string(APPEND CMAKE_EXE_LINKER_FLAGS_DEBUG " /DEBUG")
    string(APPEND CMAKE_SHARED_LINKER_FLAGS_DEBUG " /DEBUG")
    string(APPEND CMAKE_C_FLAGS_RELEASE " /O2 /DNDEBUG")
    string(APPEND CMAKE_CXX_FLAGS_RELEASE " /O2 /DNDEBUG")
else()
    string(APPEND CMAKE_C_FLAGS_DEBUG " -g")
    string(APPEND CMAKE_CXX_FLAGS_DEBUG " -g")
    set(CMAKE_C_FLAGS_RELEASE "-O2 -DNDEBUG")
    set(CMAKE_CXX_FLAGS_RELEASE "-O2 -DNDEBUG")
endif()

# -----------------------------
# Collect sources
# -----------------------------
file(GLOB_RECURSE C_SOURCES "${SOURCES_DIR}/*.c")
file(GLOB_RECURSE H_SOURCES "${SOURCES_DIR}/*.h")
file(GLOB_RECURSE ISPC_SOURCES "${SOURCES_DIR}/*.ispc")

if(NOT C_SOURCES)
    message(WARNING "No C sources found under ${SOURCES_DIR}")
endif()
if(NOT H_SOURCES)
    message(WARNING "No headers found under ${SOURCES_DIR}")
endif()
if(NOT ISPC_SOURCES)
    message(WARNING "No ISPC sources found under ${SOURCES_DIR}")
endif()

# -----------------------------
# Create directories for ISPC output
# -----------------------------
set(ISPC_GENERATED_DIR "${SOURCES_DIR}/AutoGenerated")
set(ISPC_BUILD_DIR "${CMAKE_BINARY_DIR}/ispc")
file(MAKE_DIRECTORY "${ISPC_GENERATED_DIR}")
file(MAKE_DIRECTORY "${ISPC_BUILD_DIR}")

# -----------------------------
# ISPC compilation flags
# -----------------------------
if(CMAKE_BUILD_TYPE STREQUAL "Debug")
    set(ISPC_FLAGS -O0 -g)
else()
    set(ISPC_FLAGS -O2)
endif()

# -----------------------------
# Generate ISPC headers at configure time
# -----------------------------
set(ISPC_HEADERS "")
set(ISPC_OBJECTS "")

# --- Compile ISPC files into object files ---
foreach(ISPC_FILE ${ISPC_SOURCES})
    get_filename_component(FILE_WE ${ISPC_FILE} NAME_WE)
    set(H_FILE "${SOURCES_DIR}/AutoGenerated/${FILE_WE}.h")
    set(OBJ_FILE "${CMAKE_BINARY_DIR}/ispc/${FILE_WE}.obj")

    execute_process(
        COMMAND "${ISPC_EXECUTABLE}" "${ISPC_FILE}" --target=sse2-i32x4 --pic ${ISPC_FLAGS} -h "${H_FILE}"
        RESULT_VARIABLE ISPC_RESULT
        ERROR_VARIABLE ISPC_ERR
    )
    if(NOT ISPC_RESULT EQUAL 0)
        message(FATAL_ERROR "ISPC header generation failed for ${ISPC_FILE}:\n${ISPC_ERR}")
    endif()

    add_custom_command(
        OUTPUT "${OBJ_FILE}"
        COMMAND "${ISPC_EXECUTABLE}" "${ISPC_FILE}" --target=sse2-i32x4 --pic ${ISPC_FLAGS} -o "${OBJ_FILE}"
        DEPENDS "${ISPC_FILE}" "${H_FILE}"
        COMMENT "Compiling ISPC object: ${ISPC_FILE}"
        VERBATIM
    )

    list(APPEND ISPC_HEADERS "${H_FILE}")
    list(APPEND ISPC_OBJECTS "${OBJ_FILE}")
endforeach()

# --- Static library for ISPC ---
add_library(ispc_static STATIC ${ISPC_OBJECTS})
target_include_directories(ispc_static PUBLIC "${SOURCES_DIR}/AutoGenerated")

SET_TARGET_PROPERTIES(
    ispc_static
    PROPERTIES
    LINKER_LANGUAGE C 
)

# -----------------------------
# Add executable
# -----------------------------
add_executable(tbmp_server
    ${C_SOURCES}
    ${H_SOURCES}
    ${ISPC_HEADERS}   # show generated headers in VS
    ${ISPC_SOURCES} 
)

# Include directories
target_include_directories(tbmp_server PRIVATE
    "${SOURCES_DIR}"
)

# -----------------------------
# Platform-specific flags
# -----------------------------
if(UNIX)
    target_compile_options(tbmp_server PRIVATE "-m64")
elseif(WIN32 AND MSVC)
    target_compile_options(tbmp_server PRIVATE /fp:fast /Oi)
endif()

# -----------------------------
# Required libraries
# -----------------------------
target_link_libraries(tbmp_server PRIVATE Threads::Threads ispc_static)

# -----------------------------
# Organize source tree in Visual Studio
# -----------------------------
source_group(TREE ${SOURCES_DIR} PREFIX Sources FILES
    ${C_SOURCES}
    ${H_SOURCES}
    ${ISPC_SOURCES}
    ${ISPC_HEADERS}
)

# -----------------------------
# Visual Studio debug setup
# -----------------------------
if(MSVC)
    set_target_properties(tbmp_server PROPERTIES
        VS_DEBUGGER_WORKING_DIRECTORY "${BIN_DIR}/$<CONFIG>"
    )
    set_property(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT tbmp_server)
endif()

# -----------------------------
# Build info
# -----------------------------
message(STATUS "Build type: ${CMAKE_BUILD_TYPE}")
message(STATUS "Sources directory: ${SOURCES_DIR}")
message(STATUS "ISPC generated headers: ${ISPC_GENERATED_DIR}")
message(STATUS "ISPC object directory: ${ISPC_BUILD_DIR}")
message(STATUS "ISPC executable: ${ISPC_EXECUTABLE}")
